%{
import "package:binary_declarations/binary_declarations.dart";

ArrayTypeSpecification _createArray(TypeSpecification type, List dimensions) {  
  return new ArrayTypeSpecification(dimensions: dimensions, type: type);
}

String _createInt(List parts) {
  var list = [];
  for (var part in parts) {
    if (part != null) {
      list.add(part);
    }
  }

  return list.join(" ");
}

List _createParameters(List parameters, ParameterDeclaration vaList) {    
  var list = <ParameterDeclaration>[];
  list.addAll(parameters);
  if (vaList != null) {
    list.add(vaList);  
  }
  
  return list;
}

PointerTypeSpecification _createPointer(TypeSpecification type, List stars) {    
  for (var star in stars) {
    type = new PointerTypeSpecification(type: type);
  }
  
  return type;
}
}%

# Nonterminals

Declarations <-
  SPACING? DeclarationList? EOF { $$ = $2; }

DeclarationList <-
  Declaration*

Declaration <-
  FunctionDeclartion SEMICOLON+ { $$ = $1; }
  / TypedefDeclaration SEMICOLON+ { $$ = $1; }
  / VariableDeclaration SEMICOLON+ { $$ = $1; }
  / StructDeclaration SEMICOLON+ { $$ = $1; }
  / EnumDeclaration SEMICOLON+ { $$ = $1; }
  / SEMICOLON+ { $$ = new EmptyDeclaration(); }

TypedefDeclaration <-
  TYPEDEF TypedefTypeDeclaration { $$ = $2; }

TypedefTypeDeclaration <-
  TypedefArrayTypeDeclaration
  / TypedefTaggedTypeDeclaration
  / TypedefSimpleTypeDeclaration

TypedefTaggedTypeDeclaration <-
  Attributes? TaggedTypeDeclaration IDENTIFIER { $$ = new TypedefDeclaration(name: $3, type: $2.type); }

TaggedTypeDeclaration <-
  StructDeclaration
  / EnumDeclaration

StructDeclaration <-
  (ID_STRUCT / ID_UNION) IDENTIFIER? OPEN_BRACE StructMemberList? CLOSE_BRACE { $$ = new StructureDeclaration(kind: $1, members: $4, tag: $2); }

StructMemberList <-
  (StructMember)*

StructMember <-
  VariableDeclaration SEMICOLON { $$ = $1; }

VariableDeclaration <-
  ArrayVariableDeclaration
  / TaggedTypeVariableDeclaration
  / PointerVariableDeclaration
  / SimpleVariableDeclaration

TaggedTypeVariableDeclaration <-
  (StructDeclaration / EnumDeclaration) IDENTIFIER { $$ = new VariableDeclaration(name: $2, type: $1.type); }

# Lexemes

ArrayVariableDeclaration <-
  Type IDENTIFIER DIMENSIONS { $$ = new VariableDeclaration(name: $2, type: _createArray($1, $3)); }

Attributes <-
  ATTRIBUTE OPEN_PAREN OPEN_PAREN AttributeList? CLOSE_PAREN CLOSE_PAREN { $$ = new BinaryAttributes($4); }

CLOSE_BRACE <-
  '}' SPACING { $$ = $1; }

EOF <-
  !.

EnumDeclaration <-
  ID_ENUM IDENTIFIER? OPEN_BRACE EnumValueList CLOSE_BRACE { $$ = new EnumDeclaration(kind: $1, tag: $2, values: $4); }

FunctionDeclartion <-
  IDENTIFIER OPEN_PAREN FunctionParameters? CLOSE_PAREN { $$ = new FunctionDeclaration(name: $1, parameters: $3); }
  / Type IDENTIFIER OPEN_PAREN FunctionParameters? CLOSE_PAREN { $$ = new FunctionDeclaration(name: $2, parameters: $4, returnType: $1); }

IDENTIFIER <-
  !ReservedWord IDENT_START IDENT_CONT* SPACING { $$ = _flatten([$2, $3]).join(); }

ID_STRUCT <-
  'struct' !IDENT_PART SPACING { $$ = $1; }

ID_UNION <-
  'union' !IDENT_PART SPACING { $$ = $1; }

OPEN_BRACE <-
  '{' SPACING { $$ = $1; }

PointerVariableDeclaration <-
  PointerType IDENTIFIER { $$ = new VariableDeclaration(name: $2, type: $1); }

SEMICOLON <-
  ';' SPACING { $$ = $1; }

SPACING <-
  (SPACE / COMMENT)*

SimpleVariableDeclaration <-
  Type IDENTIFIER { $$ = new VariableDeclaration(name: $2, type: $1); }

TYPEDEF <-
  'typedef' SPACING { $$ = $1; }

TypedefArrayTypeDeclaration <-
  Attributes? Type IDENTIFIER DIMENSIONS { $$ = new TypedefDeclaration(name: $3,type: _createArray($2, $4)); }

TypedefSimpleTypeDeclaration <-
  Attributes? Type IDENTIFIER { $$ = new TypedefDeclaration(attributes: $1, name: $3, type: $2); }

# Morphemes

ASSIGN <-
  '=' SPACING { $$ = $1; }

ASTERISK <-
  '*' SPACING { $$ = $1; }

ATTRIBUTE <-
  '__attribute__' SPACING { $$ = $1; }

ArrayParameter <-
  Type IDENTIFIER? DIMENSIONS { $$ = new ParameterDeclaration(name: $2, type: _createArray($1, $3)); }

ArrayType <-
  TypeName DIMENSIONS { $$ = _createArray($1, $2); }

Attribute <-
  IDENT OPEN_PAREN AttributeParameterList? CLOSE_PAREN { $$ = new BinaryAttribute($1, $3); }
  / IDENT { $$ = new BinaryAttribute($1); }

AttributeList <-
  Attribute (COMMA Attribute)* { $$ = _list($1, $2); }

AttributeParameter <-
  IDENT
  / NUMBER

AttributeParameterList <-
  AttributeParameter (COMMA AttributeParameter)* { $$ = _list($1, $2); }

CLOSE_BRACKET <-
  ']' SPACING { $$ = $1; }

CLOSE_PAREN <-
  ')' SPACING { $$ = $1; }

COMMA <-
  ',' SPACING { $$ = $1; }

COMMENT <-
  '//' (!EOL .)* (EOL / EOF)

CONST <-
  'const' SPACING { $$ = $1; }

ConstPointerParameter <-
  CONST? PointerType IDENTIFIER? { $$ = new ParameterDeclaration(name: $3, type: $2); }

DIMENSION <-
  OPEN_BRACKET Dimension? CLOSE_BRACKET { $$ = $2; }

DIMENSIONS <-
  DIMENSION+

Dimension <-
  !ZERO (OCTAL_NUMBER / INTEGER) { $$ = $2; }

ELLIPSIS <-
  '...' SPACING { $$ = $1; }

EOL <-
  '\r\n'
  / [\n\r]

EnumValue <-
  IDENTIFIER ASSIGN NUMBER { $$ = new EnumValueDeclaration(name: $1, value: $3); }
  / IDENTIFIER { $$ = new EnumValueDeclaration(name: $1); }

EnumValueList <-
  EnumValue (COMMA EnumValue)* COMMA? { $$ = _list($1, $2); }

FunctionParameter <-
  ArrayParameter
  / ConstPointerParameter
  / PointerParameter
  / SimpleParameter

FunctionParameterList <-
  FunctionParameter (COMMA FunctionParameter)* { $$ = _list($1, $2); }

FunctionParameters <-
  FunctionParameterList (COMMA VaListParameter { $$ = $2; })? { $$ = _createParameters($1, $2); }

IDENT <-
  IDENT_START IDENT_CONT* SPACING { $$ = _flatten([$1, $2]).join(); }

IDENT_CONT <-
  IDENT_START
  / [0-9]

IDENT_PART <-
  IDENT_START
  / IDENT_CONT

IDENT_START <-
  [A-Z_a-z_]

ID_CHAR <-
  'char' !IDENT_PART SPACING { $$ = $1; }

ID_DOUBLE <-
  'double' !IDENT_PART SPACING { $$ = $1; }

ID_ENUM <-
  'enum' !IDENT_PART SPACING { $$ = $1; }

ID_FLOAT <-
  'float' !IDENT_PART SPACING { $$ = $1; }

ID_INT <-
  'int' !IDENT_PART SPACING { $$ = $1; }

ID_LONG <-
  'long' !IDENT_PART SPACING { $$ = $1; }

ID_SHORT <-
  'short' !IDENT_PART SPACING { $$ = $1; }

ID_SIGNED <-
  'signed' !IDENT_PART SPACING { $$ = $1; }

ID_UNSIGNED <-
  'unsigned' !IDENT_PART SPACING { $$ = $1; }

ID_VOID <-
  'void' !IDENT_PART SPACING { $$ = $1; }

INTEGER <-
  SIGN [0-9]+ SPACING { $$ = $1 * int.parse($2.join()); }

LongType <-
  ID_LONG ID_LONG { $$ = _createInt([$1, $2]); }
  / ID_LONG

MINUS <-
  '-' SPACING { $$ = $1; }

NUMBER <-
  OCTAL_NUMBER
  / INTEGER

OCTAL_NUMBER <-
  SIGN [0] [0-9]+ { $$ = $1 * int.parse(_flatten([$2, $3]).join(), radix: 8); }

OPEN_BRACKET <-
  '[' SPACING { $$ = $1; }

OPEN_PAREN <-
  '(' SPACING { $$ = $1; }

PLUS <-
  '+' SPACING { $$ = $1; }

PointerParameter <-
  PointerType IDENTIFIER? { $$ = new ParameterDeclaration(name: $2, type: $1); }

PointerType <-
  TypeName ASTERISK+ { $$ = _createPointer($1, $2); }

ReservedWord <-
  ('signed' / 'unsigned' / 'char' / 'int' / 'short' / 'long' / 'float' / 'double' / 'enum' / 'struct' / 'typedef' / 'union' / 'void') !(IDENT_START / IDENT_CONT) { $$ = $1; }

SIGN <-
  (PLUS / MINUS)? { $$ = $1 == "-" ? -1 : 1; }

SPACE <-
  [\t-\n\r ]

SimpleParameter <-
  Type IDENTIFIER? { $$ = new ParameterDeclaration(name: $2, type: $1); }

Type <-
  PointerType
  / ArrayType
  / TypeName

TypeName <-
  IDENTIFIER { $$ = new TypedefTypeSpecification(name: $1); }
  / TypeSign? (ID_CHAR / ID_INT) { $$ = new IntegerTypeSpecification(name: _createInt([$1, $2])); }
  / TypeSign? (ID_SHORT / LongType) ID_INT? { $$ = new IntegerTypeSpecification(name: _createInt([$1, $2, $3])); }
  / ID_FLOAT { $$ = new FloatTypeSpecification(kind: "float"); }
  / ID_DOUBLE { $$ = new FloatTypeSpecification(kind: "double"); }
  / ID_ENUM IDENTIFIER { $$ = new TaggedTypeSpecification(kind: "enum", tag: $2); }
  / ID_STRUCT IDENTIFIER { $$ = new TaggedTypeSpecification(kind: "struct", tag: $2); }
  / ID_UNION IDENTIFIER { $$ = new TaggedTypeSpecification(kind: "union", tag: $2); }
  / ID_VOID { $$ = new VoidTypeSpecification(); }

TypeSign <-
  ID_SIGNED
  / ID_UNSIGNED

VaListParameter <-
  ELLIPSIS { $$ = new ParameterDeclaration(type: new VaListTypeSpecification()); }

ZERO <-
  [0]+ ![0-9]+ { $$ = 0; }

