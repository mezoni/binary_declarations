%{
import "package:binary_declarations/binary_declarations.dart";

ArrayTypeSpecification _createArray(TypeSpecification type, ArrayDimensions dimensions) {  
  return new ArrayTypeSpecification(dimensions: dimensions, type: type);
}

String _createInt(List parts) {
  var list = [];
  for (var part in parts) {
    if (part != null) {
      list.add(part);
    }
  }

  return list.join(" ");
}

List _createParameters(List parameters, ParameterDeclaration vaList) {    
  var list = <ParameterDeclaration>[];
  list.addAll(parameters);
  if (vaList != null) {
    list.add(vaList);  
  }
  
  return list;
}

PointerTypeSpecification _createPointerType(TypeSpecification type, List stars) {    
  for (var star in stars) {
    type = new PointerTypeSpecification(type: type);
  }
  
  return type;
}

int _parseInt(String sign, String digits, int radix) {
  if (sign == null) {
    sign = "";
  }
  return int.parse("$sign$digits", radix: radix);
}

}%

### Sentences (nonterminals) ###

Declarations <-
  LEADING_SPACES? DeclarationList? EOF { $$ = $2; }

DeclarationList <-
  Declaration*

Declaration <-
  FunctionDeclartion SEMICOLON+ { $$ = $1; }
  / TypedefDeclaration SEMICOLON+ { $$ = $1; }
  / VariableDeclaration SEMICOLON+ { $$ = $1; }
  / StructDeclaration SEMICOLON+ { $$ = $1; }
  / EnumDeclaration SEMICOLON+ { $$ = $1; }
  / SEMICOLON+ { $$ = new EmptyDeclaration(); }

FunctionDeclartion <-
  Metadata? Type Identifier FunctionParameters { $$ = new FunctionDeclaration(metadata: $1, identifier: $3, parameters: $4, returnType: $2); }

Metadata <-
  AttributeSpecifier+ { $$ = new Metadata($1); }

AttributeSpecifier <-
  ATTRIBUTE OPEN_PAREN OPEN_PAREN AttributeList? CLOSE_PAREN CLOSE_PAREN { $$ = new AttributeList($4); }

AttributeList <-
  Attribute (COMMA Attribute)* { $$ = _list($1, $2); }

Attribute <-
  IDENT OPEN_PAREN AttributeParameterList? CLOSE_PAREN { $$ = new Attribute($1, $3); }
  / IDENT { $$ = new Attribute($1); }

AttributeParameterList <-
  AttributeParameter (COMMA AttributeParameter)* { $$ = _list($1, $2); }

AttributeParameter <-
  IDENT
  / NUMBER

Type <-
  PointerType
  / ArrayType
  / TypeName

PointerType <-
  TypeName ASTERISK+ { $$ = _createPointerType($1, $2); }

TypeName <-
  TypeQualifiers? IDENTIFIER Metadata? { $$ = new DefinedTypeSpecification(metadata: $3, name: $2, qualifiers: $1); }
  / TypeQualifiers? SIGNEDNESS? (SHORT / LONG_TYPE) INT? Metadata? { $$ = new IntegerTypeSpecification(metadata: $5, name: _createInt([$2, $3, $4]), qualifiers: $1); }
  / TypeQualifiers? SIGNEDNESS? (CHAR / INT / SHORT / LONG_TYPE) Metadata? { $$ = new IntegerTypeSpecification(metadata: $4, name: _createInt([$2, $3]), qualifiers: $1); }
  / TypeQualifiers? SIGNEDNESS Metadata? { $$ = new IntegerTypeSpecification(metadata: $3, name: $2, qualifiers: $1); }
  / TypeQualifiers? (FLOAT / DOUBLE) Metadata? { $$ = new FloatTypeSpecification(metadata: $3, name: $2, qualifiers: $1); }
  / TypeQualifiers? VOID Metadata? { $$ = new VoidTypeSpecification(metadata: $3, qualifiers: $1); }
  / StructTypeName
  / EnumTypeName

StructTypeName <-
  TypeQualifiers? (STRUCT / UNION) Metadata? IDENTIFIER? { $$ = new TaggedTypeSpecification(kind: $2, metadata: $3, qualifiers: $1, tag: $4); }

EnumTypeName <-
  TypeQualifiers? ENUM Metadata? IDENTIFIER? { $$ = new TaggedTypeSpecification(kind: $2, metadata: $3, qualifiers: $1, tag: $4); }

ArrayType <-
  TypeName ArrayDimensions { $$ = _createArray($1, $2); }

ArrayDimensions <-
  Dimension+ Metadata? { $$ = new ArrayDimensions(dimensions: $1, metadata: $2); }

Dimension <-
  OPEN_BRACKET ArraySize? CLOSE_BRACKET { $$ = $2; }

ArraySize <-
  !ZERO_NUMBER POSITIVE_NUMBER { $$ = $2; }

Identifier <-
  IDENTIFIER { $$ = new Identifier(name: $1); }

FunctionParameters <-
  OPEN_PAREN FunctionParameterList? CLOSE_PAREN Metadata? { $$ = new FunctionParameters(metadata: $4, parameters: $2); }

FunctionParameterList <-
  FunctionParameterListNoVaList (COMMA VA_LIST_PARAMETER { $$ = $2; })? { $$ = _createParameters($1, $2); }

FunctionParameterListNoVaList <-
  FunctionParameter (COMMA FunctionParameter)* { $$ = _list($1, $2); }

FunctionParameter <-
  ArrayParameter
  / PointerParameter
  / SimpleParameter

ArrayParameter <-
  Metadata? Type Identifier? ArrayDimensions { $$ = new ParameterDeclaration(identifier: $3, metadata: $1, type: _createArray($2, $4)); }

PointerParameter <-
  Metadata? PointerType IdentifierMD? { $$ = new ParameterDeclaration(identifier: $3, metadata: $1, type: $2); }

IdentifierMD <-
  IDENTIFIER Metadata? { $$ = new Identifier(metadata: $2, name: $1); }

SimpleParameter <-
  Metadata? Type IdentifierMD? { $$ = new ParameterDeclaration(identifier: $3, metadata: $1, type: $2); }

TypedefDeclaration <-
  Metadata? TypedefSpecifier TypedefType TypeSynonymList { $$ = new TypedefDeclaration(metadata: $1, synonyms: $4, typedef: $2, type: $3); }

TypedefSpecifier <-
  TYPEDEF Metadata? { $$ = new TypedefSpecifier(metadata: $2); }

TypedefType <-
  StructType
  / EnumType
  / TypeName

StructType <-
  StructTypeName OPEN_BRACE StructMemberList? CLOSE_BRACE Metadata? { $$ = new StructureTypeSpecification(metadata: $5, taggedType: $1, members: $3); }

StructMemberList <-
  (StructMember)*

StructMember <-
  BitFieldParameter SEMICOLON { $$ = $1; }
  / ArrayParameter SEMICOLON { $$ = $1; }
  / TaggedTypeParameter SEMICOLON { $$ = $1; }
  / PointerParameter SEMICOLON { $$ = $1; }
  / SimpleParameter SEMICOLON { $$ = $1; }

BitFieldParameter <-
  Type Identifier? COLON POSITIVE_NUMBER { $$ = new ParameterDeclaration(identifier: $2, type: $1, width: $4); }

TaggedTypeParameter <-
  (StructDeclaration / EnumDeclaration) IdentifierMD { $$ = new ParameterDeclaration(identifier: $2, type: $1.type); }

StructDeclaration <-
  StructType { $$ = new StructureDeclaration(type: $1); }

EnumDeclaration <-
  EnumType { $$ = new EnumDeclaration(type: $1); }

EnumType <-
  EnumTypeName OPEN_BRACE EnumValueList CLOSE_BRACE Metadata? { $$ = new EnumTypeSpecification(metadata: $5, taggedType: $1, values: $3); }

EnumValueList <-
  EnumValue (COMMA EnumValue)* COMMA? { $$ = _list($1, $2); }

EnumValue <-
  IDENTIFIER ASSIGN NUMBER { $$ = new EnumValueDeclaration(name: $1, value: $3); }
  / IDENTIFIER { $$ = new EnumValueDeclaration(name: $1); }

TypeSynonymList <-
  TypeSynonym (COMMA TypeSynonym)* { $$ = _list($1, $2); }

TypeSynonym <-
  ArrayTypeSynonym
  / PointerTypeSynonym
  / SimpleTypeSynonym

ArrayTypeSynonym <-
  ArrayTypeElementSynonym ArrayDimensions { $$ = new ArrayTypeSpecification(dimensions: $2, type: $1); }

ArrayTypeElementSynonym <-
  PointerTypeSynonymNoMetadata
  / SimpleTypeSynonymNoMetadata

PointerTypeSynonymNoMetadata <-
  TypeQualifiers? ASTERISK Metadata? TypeSynonymNoMetadata { $$ = new PointerTypeSpecification(metadata: $3, qualifiers: $1, type: $4); }

TypeSynonymNoMetadata <-
  ArrayTypeSynonym
  / PointerTypeSynonymNoMetadata
  / SimpleTypeSynonymNoMetadata

SimpleTypeSynonymNoMetadata <-
  TypeQualifiers? IDENTIFIER { $$ = new DefinedTypeSpecification(qualifiers: $1, name: $2); }

PointerTypeSynonym <-
  TypeQualifiers? ASTERISK Metadata? TypeSynonym { $$ = new PointerTypeSpecification(metadata: $3, qualifiers: $1, type: $4); }

SimpleTypeSynonym <-
  TypeQualifiers? IDENTIFIER Metadata? { $$ = new DefinedTypeSpecification(metadata: $3, qualifiers: $1, name: $2); }

VariableDeclaration <-
  ArrayVariableDeclaration
  / TaggedTypeVariableDeclaration
  / PointerVariableDeclaration
  / SimpleVariableDeclaration

ArrayVariableDeclaration <-
  Metadata? Type Identifier ArrayDimensions { $$ = new VariableDeclaration(identifier: $3, metadata: $1, type: _createArray($2, $4)); }

TaggedTypeVariableDeclaration <-
  Metadata? (StructDeclaration / EnumDeclaration) IdentifierMD { $$ = new VariableDeclaration(identifier: $3, metadata: $1, type: $2.type); }

PointerVariableDeclaration <-
  Metadata? PointerType IdentifierMD { $$ = new VariableDeclaration(identifier: $3, metadata: $1, type: $2); }

SimpleVariableDeclaration <-
  Metadata? Type IdentifierMD { $$ = new VariableDeclaration(identifier: $3, metadata: $1, type: $2); }

### Lexemes (tokens) ###

ASSIGN <-
  '=' SPACING { $$ = $1; }

ASTERISK <-
  '*' SPACING { $$ = $1; }

ATTRIBUTE <-
  '__attribute__' SPACING { $$ = $1; }

CHAR <-
  'char' !IDENT_PART SPACING { $$ = $1; }

CLOSE_BRACE <-
  '}' SPACING { $$ = $1; }

CLOSE_BRACKET <-
  ']' SPACING { $$ = $1; }

CLOSE_PAREN <-
  ')' SPACING { $$ = $1; }

COLON <-
  ':' SPACING { $$ = $1; }

COMMA <-
  ',' SPACING { $$ = $1; }

DOUBLE <-
  'double' !IDENT_PART SPACING { $$ = $1; }

ENUM <-
  'enum' !IDENT_PART SPACING { $$ = $1; }

EOF <-
  !.

FLOAT <-
  'float' !IDENT_PART SPACING { $$ = $1; }

IDENT <-
  IDENT_START IDENT_CONT* SPACING { $$ = _flatten([$1, $2]).join(); }

IDENTIFIER <-
  !RESERVED_WORD IDENT_START IDENT_CONT* SPACING { $$ = _flatten([$2, $3]).join(); }

INT <-
  'int' !IDENT_PART SPACING { $$ = $1; }

LEADING_SPACES <-
  SPACING

LONG_TYPE <-
  LONG LONG { $$ = _createInt([$1, $2]); }
  / LONG

NUMBER <-
  OCTAL_NUMBER
  / INTEGER

OPEN_BRACE <-
  '{' SPACING { $$ = $1; }

OPEN_BRACKET <-
  '[' SPACING { $$ = $1; }

OPEN_PAREN <-
  '(' SPACING { $$ = $1; }

POSITIVE_NUMBER <-
  !('-' SPACING) (OCTAL_NUMBER / INTEGER) { $$ = $2; }

SEMICOLON <-
  ';' SPACING { $$ = $1; }

SHORT <-
  'short' !IDENT_PART SPACING { $$ = $1; }

SIGNEDNESS <-
  SIGNED
  / UNSIGNED

STRUCT <-
  'struct' !IDENT_PART SPACING { $$ = $1; }

TYPEDEF <-
  'typedef' SPACING { $$ = $1; }

TypeQualifiers <-
  TypeQualifier* { $$ = new TypeQualifierList($1); }

UNION <-
  'union' !IDENT_PART SPACING { $$ = $1; }

VA_LIST_PARAMETER <-
  ELLIPSIS { $$ = new ParameterDeclaration(type: new VaListTypeSpecification()); }

VOID <-
  'void' !IDENT_PART SPACING { $$ = $1; }

ZERO_NUMBER <-
  ZERO SPACING { $$ = $1; }

### Morphemes ###

COMMENT <-
  '//' (!EOL .)* (EOL / !.)

CONST <-
  'const' SPACING { $$ = $1; }

ELLIPSIS <-
  '...' SPACING { $$ = $1; }

EOL <-
  '\r\n'
  / [\n\r]

IDENT_CONT <-
  IDENT_START
  / [0-9]

IDENT_PART <-
  IDENT_START
  / IDENT_CONT

IDENT_START <-
  [A-Z_a-z]

INTEGER <-
  SIGN? [0-9]+ SPACING { $$ = _parseInt($1, $2.join(), 10); }

LONG <-
  'long' !IDENT_PART SPACING { $$ = $1; }

MINUS <-
  '-' SPACING { $$ = $1; }

OCTAL_NUMBER <-
  SIGN? [0] [0-9]+ { $$ = _parseInt($1, _flatten([$2, $3]).join(), 8); }

PLUS <-
  '+' SPACING { $$ = $1; }

RESERVED_WORD <-
  ('auto' / 'break' / 'case' / 'char' / 'const' / 'continue' / 'default' / 'do' / 'double' / 'else' / 'enum' / 'extern' / 'float' / 'for' / 'goto' / 'if' / 'inline' / 'int' / 'long' / 'register' / 'restrict' / 'return' / 'short' / 'signed' / 'sizeof' / 'static' / 'struct' / 'switch' / 'typedef' / 'union' / 'unsigned' / 'void' / 'volatile' / 'while' / '_Bool' / '_Complex' / '_Imaginary') !(IDENT_START / IDENT_CONT) { $$ = $1; }

SIGN <-
  PLUS
  / MINUS

SIGNED <-
  'signed' !IDENT_PART SPACING { $$ = $1; }

SPACE <-
  [\t-\n\r ]

SPACING <-
  (SPACE / COMMENT)*

TypeQualifier <-
  CONST

UNSIGNED <-
  'unsigned' !IDENT_PART SPACING { $$ = $1; }

ZERO <-
  [0]+ ![0-9]+ { $$ = 0; }

